plugins {
    id 'java'
    id 'maven-publish'
    // id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'ru.c0rex86'
version = '1.0-SNAPSHOT'
description = 'Advanced GUI library for Minecraft plugins'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'org.yaml:snakeyaml:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

processResources {
    def props = [
            'version': version,
            'description': project.description,
            'apiVersion': '1.21'
    ]
    inputs.properties props
    filesMatching('plugin.yml') {
        expand props
    }
}

/* 
shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
    
    relocate 'org.bstats', 'ru.c0rex86.lingui.lib.bstats'
}

build {
    dependsOn shadowJar
}
*/

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

// GitHub Packages publishing configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'lingui'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'LinGUi'
                description = 'Advanced GUI library for Minecraft plugins'
                url = 'https://github.com/c0rex86/LinGUi'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'c0rex86'
                        name = 'c0re'
                        email = 'c0rex86@gmail.com'
                        url = 'https://c0rex86.ru'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/c0rex86/LinGUi")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
} 